(* Program structure *)

Program
    = { GlobalVariable | TypeDef | FnDef }, MainFunction;

GlobalVariable
    = { VarDecl | VarDef };
MainFunction
    = "fn", "main()", "->", "int", Block;


(* Lexical conventions *)

nonZeroDigit 
    = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9";
digit 
    = "0" | nonZeroDigit;
letter 
    = "[a-zA-Z]";
character
    = "'", ? Any ASCII character ?, "'";
identifier 
    = ( letter | "_" ), { letter | digit | "_" };
typeIdentifier
    = identifier;
primitiveType
    = "int" 
    | "float" 
    | "char" 
    | "bool" 
    | "string";


(* LITERALS *)

literal 
    = integerLiteral 
    | floatLiteral 
    | stringLiteral 
    | boolLiteral 
    | variantLiteral 
    | objectLiteral;

integerLiteral 
    = "0" | nonZeroDigit, { digit };
floatLiteral 
    = integerLiteral, ".", digit, { digit };
charLiteral 
    = "'", ( character | escape_sequence ), "'";    (* SINGLE QUOTES *)
boolLiteral 
    = "true" | "false";
stringLiterals
    = '"', { character | escape_sequence } , '"';   (* DOUBLE QUOTES *)

variantLiteral 
    = typeIdentifier, "{", expression, [ "," ], "}";

objectLiteral
    = typeIdentifier, "{" , fieldValues, [ "," ], "}";
fieldValues 
    = fieldValue, { ",", fieldValue };
fieldValue 
    = [ identifier, ":" ], expression;

escape_sequence 
    = "\" , ( "n" | "t" | "\" | '"' | "'" | "0");


(* EXPRESSIONS *)

expression 
    = logicOrExpr;

logicOrExpr 
    = logicAndExpr, { logicOrOp, logicAndExpr };
logicAndExpr 
    = equalityExpr, { equalityOp, equalityExpr };
equalityExpr
    = relationalExpr, { equalityOp, relationalExpr };
relationalExpr 
    = additiveExpr, { relationalOp, additiveExpr };
additiveExpr 
    = multiplicativeExpr, { additiveOp, multiplicativeExpr };
multiplicativeExpr 
    = unaryOpExpr, { multiplicativeOp, unaryOpExpr };
unaryOpExpr
    = [ unaryOp ], functionalExpr;
functionalExpr
    = primaryExpr, { functionalOp };

primaryExpr
    = parenExpr
    | castExpr
    | literal
    | identifier;

parenExpr 
    = "(", expression, ")";
castExpr
    = primitiveType, "(", expression, ")";


(* OPERATORS *)

logicOrOp                   (* prio 0 l-r *)
    = "||";

logicAndOp                  (* prio 1 l-r *)
    = "&&";

equalityOp                  (* prio 2 l-r *)
    = "==" 
    | "!=";

relationalOp                (* prio 3 l-r *)
    = "<" 
    | ">" 
    | "<=" 
    | ">=";

additiveOp                  (* prio 4 l-r *)
    = "+" 
    | "-";

multiplicativeOp            (* prio 5 l-r *)
    = "*" 
    | "/" 
    | "%";

unaryOp                     (* prio 6 r-l *)
    = "!"
    | "-";

functionalOp                (* prio 7 l-r *)
    = fnCallOp 
    | memberAccessOp 
    | variantAccessOp;

fnCallOp
    = "(", [ expression, { ",", expression } ], ")"; 
memberAccessOp 
    = ".", identifier;
variantAccessOp
    = "as", typeIdentifier;


(* DECLARATIONS *)

VarDecl 
    = identifier, ":", typeIdentifier, ";";


(* DEFINITIONS *)

VarDef
    = [ "const" ], identifier, ":", typeIdentifier, "=", expression, ";";

TypeDef
    = StructDef | VariantDef;
StructDef
    = "struct", identifier, "{", { VarDecl }, "}", ";";
VariantDef
    = "variant", identifier, "{", { typeIdentifier }, "}", ";";

FnDef
    = "fn", identifier, "(", [ parameters ], ")", [ returnTypeAnnotation ], Block;
parameters 
    = parameter, { ",", parameter };
parameter
    = [ "const" ], identifier, ":", ( typeIdentifier | functionSignature );
functionSignature
    = "(", [ parameterTypes ], ")", returnTypeAnnotation;
parameterTypes
    = parameterType, { ",", parameterType };
parameterType
    = typeIdentifier | functionSignature;
returnTypeAnnotation
    = "->", ( typeIdentifier | "void" | functionSignature );



(* STATEMENTS *)

Statement 
    = VarDecl
    | VarDef
    | TypeDef
    | FnDef
    | StdinExtraction
    | StdoutInsertion
    | AssignmentStatement
    | ExpressionStatement
    | VariantMatchStatement
    | IfStatement
    | ForStatement
    | WhileStatement
    | ContinueStatement
    | BreakStatement
    | ReturnStatement
    | Block;

StdinExtraction
    = ">>", expression, ";";
StdoutInsertion
    = "<<", expression, { "<<", expression }, ";";

AssignmentStatement
    = expression, "=", expression, ";";

ExpressionStatement
    = expression, ";";

VariantMatchStatement
    = "match", expression, "{", { variantCase }, "}";
variantCase
    = "case", typeIdentifier, "->", Block;

IfStatement
    = "if", { elifClause }, [ elseClause ];
elifClause
    = "elif", expression, Block;
elseClause
    = "else", Block;

WhileStatement
    = "while", expression, Block;

ForStatement
    = "for", identifier, "in", range, Block;
range
    = expression, "until", expression;

ReturnStatement
    = "return", [ expression ], ";";

BreakStatement
    = "break", ";";
ContinueStatement
    = "continue", ";";

Block
    = "{", { Statement }, "}";
