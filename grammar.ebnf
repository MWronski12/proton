(* Program structure *)

Program
    = { Definition };


(* Lexical conventions *)

identifier                              (* RETURNED BY LEXER *) 
    = [_a-zA-Z][_a-zA-Z0-9]*;           
typeIdentifier
    = identifier;
primitiveType                           (* RETURNED BY LEXER *)
    = "int"
    | "float"
    | "char"
    | "bool"
    | "string";


(* LITERALS *)

literal                       (* RETURNED BY LEXER *)
    = int
    | float
    | bool 
    | char
    | string;

object
    = "{", [ objectMembers ], "}";
objectMembers
    =  objectMember, {",", objectMember };
objectMember
    = identifier, ":", expression;


(* EXPRESSIONS *)

expression 
    = logicOrExpr;

logicOrExpr 
    = logicAndExpr, { logicOrOp, logicAndExpr };
logicAndExpr 
    = equalityExpr, { logicAndOp, equalityExpr };
equalityExpr
    = relationalExpr, { equalityOp, relationalExpr };
relationalExpr 
    = additiveExpr, { relationalOp, additiveExpr };
additiveExpr 
    = multiplicativeExpr, { additiveOp, multiplicativeExpr };
multiplicativeExpr 
    = unaryOpExpr, { multiplicativeOp, unaryOpExpr };
unaryOpExpr
    = [ unaryOp ], functionalExpr;
functionalExpr
    = primaryExpr, { funcExprPostfix };

primaryExpr
    = identifierExpr
    | literal
    | object
    | parenExpr
    | castExpr;

identifierExpr
    = identifier;

parenExpr 
    = "(", expression, ")";
castExpr
    = primitiveType, "(", expression, ")";

                                                    
(* OPERATORS *)             (* RETURNED BY LEXER *) 

logicOrOp                   (* prio 0 l-r *)
    = "||";

logicAndOp                  (* prio 1 l-r *)
    = "&&";

equalityOp                  (* prio 2 l-r *)
    = "==" 
    | "!=";

relationalOp                (* prio 3 l-r *)
    = "<" 
    | ">" 
    | "<=" 
    | ">=";

additiveOp                  (* prio 4 l-r *)
    = "+" 
    | "-";

multiplicativeOp            (* prio 5 l-r *)
    = "*" 
    | "/" 
    | "%";

unaryOp                     (* prio 6 r-l *)
    = "!"
    | "-";

funcExprPostfix           (* prio 7 l-r *)
    = fnCallOp
    | memberAccessOp 
    | variantAccessOp;

fnCallOp
    = "(", [ expression, { ",", expression } ], ")"; 
memberAccessOp
    = ".", identifier;
variantAccessOp
    = "as", typeIdentifier;


(* DEFINITIONS *)

Definition
    = VarDef
    | ConstDef
    | StructDef
    | VariantDef
    | FnDef;

VarDef
    = "var", identifier, ":", typeIdentifier, "=", expression, ";";
ConstDef
    = "const", identifier, ":", typeIdentifier, "=", expression, ";";

StructDef
    = "struct", identifier, "{", [ structMembers ], "}", ";";
structMembers
    = structMember, { structMember };
structMember
    = identifier, ":", typeIdentifier, ";";

VariantDef
    = "variant", identifier, "{", [ variantTypes ], "}", ";";
variantTypes
    = variantType, { ",", variantType };
variantType
    = typeIdentifier

FnDef
    = "fn", identifier, "(", [ fnParams ], ")", returnTypeAnnotation, Block;
fnParams
    = fnParam, { ",", fnParam };
fnParam
    = [ "const" ], identifier, ":", typeIdentifier;
fnReturnType
    = "->", typeIdentifier;


(* STATEMENTS *)

Statement
    = Definition
    | BlockStmt
    | ExpressionOrAssignmentStmt
    | StdinExtractionStmt
    | StdoutInsertionStmt
    | VariantMatchStmt
    | IfStmt
    | ForStmt
    | WhileStmt
    | ContinueStmt
    | BreakStmt
    | ReturnStmt

StdinExtractionStmt
    = ">>", expression, ";";
StdoutInsertionStmt
    = "<<", expression, { "<<", expression }, ";";

ExpressionOrAssignmentStmt
    = expression, [ "=", expression ], ";";

VariantMatchStmt
    = "match", expression, "{", variantMatchCases, "}";
variantMatchCases
    = { variantMatchCase };
variantMatchCase
    = "case", typeIdentifier, "->", Block;

IfStmt
    = "if", { elifClause }, [ elseClause ];
elifClause
    = "elif", expression, Block;
elseClause
    = "else", Block;

ForStmt
    = "for", identifier, "in", range, Block;
Range
    = expression, "until", expression;

WhileStmt
    = "while", expression, Block;

ReturnStmt
    = "return", [ expression ], ";";

BreakStmt
    = "break", ";";
ContinueStmt
    = "continue", ";";

BlockStmt
    = "{", { Statement }, "}";
