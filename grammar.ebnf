(* Program structure *)

Program
    = { Definition };


(* Lexical conventions *)

identifier                              (* RETURNED BY LEXER *) 
    = [_a-zA-Z][_a-zA-Z0-9]*;           
typeIdentifier
    = identifier;
primitiveType                           (* RETURNED BY LEXER *)
    = "int"
    | "float"
    | "char"
    | "bool"
    | "string";


(* LITERALS *)

Literal                       (* RETURNED BY LEXER *)
    = Int
    | Float
    | Bool 
    | Char
    | String;

Object
    = "{", { ObjectMember }, "}";
ObjectMember
    = identifier, ":", expression;


(* EXPRESSIONS *)

expression 
    = logicOrExpr;

logicOrExpr 
    = logicAndExpr, { logicOrOp, logicAndExpr };
logicAndExpr 
    = equalityExpr, { logicAndOp, equalityExpr };
equalityExpr
    = relationalExpr, { equalityOp, relationalExpr };
relationalExpr 
    = additiveExpr, { relationalOp, additiveExpr };
additiveExpr 
    = multiplicativeExpr, { additiveOp, multiplicativeExpr };
multiplicativeExpr 
    = unaryOpExpr, { multiplicativeOp, unaryOpExpr };
unaryOpExpr
    = [ unaryOp ], functionalExpr;
functionalExpr
    = primaryExpr, { funcExprPostfix };

primaryExpr
    = identifierExpr
    | literal
    | object
    | parenExpr
    | castExpr;

identifierExpr
    = identifier;

parenExpr 
    = "(", expression, ")";
castExpr
    = primitiveType, "(", expression, ")";

                                                    
(* OPERATORS *)             (* RETURNED BY LEXER *) 

logicOrOp                   (* prio 0 l-r *)
    = "||";

logicAndOp                  (* prio 1 l-r *)
    = "&&";

equalityOp                  (* prio 2 l-r *)
    = "==" 
    | "!=";

relationalOp                (* prio 3 l-r *)
    = "<" 
    | ">" 
    | "<=" 
    | ">=";

additiveOp                  (* prio 4 l-r *)
    = "+" 
    | "-";

multiplicativeOp            (* prio 5 l-r *)
    = "*" 
    | "/" 
    | "%";

unaryOp                     (* prio 6 r-l *)
    = "!"
    | "-";

funcExprPostfix           (* prio 7 l-r *)
    = fnCallPostfix
    | memberAccessPostfix 
    | variantAccessPostfix;

fnCallPostfix
    = "(", [ expression, { ",", expression } ], ")"; 
memberAccessPostfix
    = ".", identifier;
variantAccessPostfix
    = "as", typeIdentifier;


(* DEFINITIONS *)

Definition
    = VarDef
    | ConstDef
    | StructDef
    | VariantDef
    | FnDef;

VarDef
    = "var", identifier, ":", typeIdentifier, "=", expression, ";";
ConstDef
    = "const", identifier, ":", typeIdentifier, "=", expression, ";";

StructDef
    = "struct", identifier, "{", { StructMember }, "}", ";";
StructMember
    = identifier, ":", typeIdentifier, ";";

VariantDef
    = "variant", identifier, "{", [ variantTypes ], "}", ";";
variantTypes
    = typeIdentifier, { ",", typeIdentifier };

FnDef
    = "fn", identifier, "(", [ fnParams ], ")", "->", typeIdentifier, Block;
fnParams
    = fnParam, { ",", fnParam };
FnParam
    = [ "const" ], identifier, ":", typeIdentifier;

(* STATEMENTS *)

Statement
    = Definition
    | BlockStmt
    | ExpressionOrAssignmentStmt
    | StdinExtractionStmt
    | StdoutInsertionStmt
    | VariantMatchStmt
    | IfStmt
    | ForStmt
    | WhileStmt
    | ContinueStmt
    | BreakStmt
    | ReturnStmt

StdinExtractionStmt
    = ">>", expression, { ">>", expression }, ";";
StdoutInsertionStmt
    = "<<", expression, { "<<", expression }, ";";

ExpressionOrAssignmentStmt
    = expression, [ "=", expression ], ";";

VariantMatchStmt
    = "match", expression, "{", { variantMatchCase }, "}";
VariantMatchCase
    = "case", typeIdentifier, "->", Block;

IfStmt
    = "if", expression, Block, { elif }, [ else ];
Elif
    = "elif", expression, Block;
else
    = "else", Block;

ForStmt
    = "for", identifier, "in", range, Block;
Range
    = expression, "until", expression;

WhileStmt
    = "while", expression, Block;

ReturnStmt
    = "return", [ expression ], ";";

BreakStmt
    = "break", ";";
ContinueStmt
    = "continue", ";";

BlockStmt
    = "{", { Statement }, "}";
